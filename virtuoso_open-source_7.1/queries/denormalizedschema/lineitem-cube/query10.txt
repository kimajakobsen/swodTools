prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
prefix ltpch: <http://extbi.lab.aau.dk/ontology/ltpch/>
prefix qb: <http://purl.org/linked-data/cube#>

select
  ?c_custkey
  ?c_companyName
  (sum(xsd:decimal(?l_lineextendedprice) * (1 - xsd:decimal(?l_linediscount))) as ?revenue)
  ?c_acctbal
  ?nation
  ?c_address
  ?c_phone
  ?c_comment
where  {
  ?li qb:dataSet ltpch:lineitemCube ;
      ltpch:l_returnflag ?l_returnflag ;
      ltpch:l_lineextendedprice ?l_lineextendedprice ;
      ltpch:l_linediscount ?l_linediscount ;
      ltpch:order_order_orderdate ?o_orderdate ;
      ltpch:order_customer_address ?c_address ;
      ltpch:order_customer_phone ?c_phone ;
      ltpch:order_customer_comment ?c_comment ;
      ltpch:order_customer_acctbal ?c_acctbal ;
      ltpch:order_customer_custkey ?c_custkey ;
      ltpch:order_customer_name ?c_companyName ;
      ltpch:order_nation_name ?nation .
   filter ((xsd:dateTime(?o_orderdate) >= xsd:dateTime("%MONTH%-01"^^xsd:date)) &&
      (xsd:dateTime(?o_orderdate) < xsd:dateTime(bif:dateadd ("month", 3, "%MONTH%-01"^^xsd:date))) &&
      (?l_returnflag = "R") 
   ) 
}
group by
  ?c_custkey
  ?c_companyName
  ?c_acctbal
  ?nation
  ?c_address
  ?c_phone
  ?c_comment
order by
  desc (sum(xsd:decimal(?l_lineextendedprice) * (1 - xsd:decimal(?l_linediscount))))
limit 20
